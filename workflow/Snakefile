import os
import sys
import math
from snakemake.utils import min_version
import pandas as pd

min_version("6.6")

SDIR = os.path.realpath(os.path.dirname(srcdir("Snakefile")))
shell.prefix(
    f"export PATH=$PWD/fastCN:$PATH; export PATH=$PWD/bin:$PATH; set -eo pipefail;"
)


samples_df = pd.read_csv(config["manifest"], sep="\t")
samples = samples_df["sample"].tolist()
reads = samples_df["reads"].tolist()
config["reads"] = dict(zip(samples, reads))

fai_df = pd.read_csv(
    f'{config["fasta"]}.fai',
    sep="\t",
    header=None,
    names=["chr", "length", "offset", "bases", "bytes"],
)
num_contigs_in_ref = fai_df.shape[0]


wildcard_constraints:
    sm="|".join(config["reads"].keys()),


include: "rules/reference.smk"
include: "rules/mapping.smk"
include: "rules/cn-calling.smk"
include: "rules/trackhub.smk"


rule all:
    input:
        expand(
            rules.make_trackdb.output,
            sample=config["reference"],
        ),
        expand(
            rules.make_bb.output, sample=config["reference"], sm=config["reads"].keys()
        ),
